# This is a very early attempt to wrap OpenARC+LLVM in a cc-like wrapper
# script.  It does not yet support all features of a traditional C compiler.
# Moreover, it does not always correctly quote command-line options it
# passes through to other commands.

use strict;
use warnings;

use Carp;
use FindBin qw($RealBin);

my $CPP = '@CPP@';
my $CC = '@CC@';
my $CETUS_OUTDIR = 'cetus_output';
my $PMEMOBJ_INCLUDES = '@PMEMOBJ_INCLUDES@';
my $PMEMOBJ_LIBDIR = '@PMEMOBJ_LIBDIR@';

my $LLVM_BIN = "$RealBin/../llvm/install/bin";
my $LLVM_LLC = "$LLVM_BIN/llc";
my $LLVM_OPT = "$LLVM_BIN/opt";

exit main(@ARGV);

sub main {
  print STDERR "$0: called with args: @ARGV\n";

  # clean up
  run("rm -rf ".shesc($CETUS_OUTDIR)
      ." && mkdir -p ".shesc($CETUS_OUTDIR)."/opt",
      "Cetus output directory cleanup");

  # parse args
  my $arcOpts = "";
  my $outOptFile;
  my $cppOpts = " -I. -D_OPENARC_";
  my $ldOpts = "";
  my $llcOpts = "";
  my $inCFileArgs = "";
  my $inOFileArgs = "";
  my $compileOnly = 0;
  for (my $argi = 0; $argi <= $#_; ++$argi) {
    my $arg = $_[$argi];
    if ($arg eq "-o") {
      die if $#_ < ++$argi;
      $outOptFile = $_[$argi];
    }
    elsif ($arg =~ /^-Warc,/) {
      # This is like gcc's "-Wl," except it passes its argument to OpenARC
      # instead of the linker.
      my $optArg = substr $arg, 6;
      $arcOpts .= " " . shesc($optArg);
    }
    elsif ($arg eq "-ffitl") {
      $arcOpts .= " -enableFaultInjection";
    }
    elsif ($arg =~ /^-D/ || $arg =~ /^-I/ || $arg =~ /^-U/) {
      my $opt = substr $arg, 0, 2;
      my $optArg = substr $arg, 2;
      if ($optArg eq "") {
        die if $#_ < ++$argi;
        $optArg = $_[$argi];
      }
      $cppOpts .= " $opt$optArg";
    }
    elsif ($arg =~ /^-[lL]/) {
      $ldOpts .= " $arg";
    }
    elsif ($arg eq "-c") {
      $compileOnly = 1;
    }
    elsif ($arg =~ /^-O/) {
      $llcOpts .= " " . shesc($arg);
    }
    elsif ($arg =~ /^-g/) {
    }
    elsif ($arg =~ /^-/) {
      die "unknown option: $arg\n";
    }
    elsif ($arg =~ /\.o$/) {
      $inOFileArgs .= " " . shesc($arg);
    }
    else {
      $inCFileArgs .= " " . shesc($arg);
    }
  }
  $cppOpts .= " -I$RealBin/../openarcrt -I$RealBin/../nvl/include";
  if ($PMEMOBJ_INCLUDES) { $cppOpts .= " -I$PMEMOBJ_INCLUDES"; }
  $ldOpts .= " -L$RealBin/../openarcrt -L$RealBin/../nvl/rt";
  if ($PMEMOBJ_LIBDIR) { $ldOpts .= " -L$PMEMOBJ_LIBDIR"; }

  # run openarc and llc
  my $outFiles = "";
  if ($inCFileArgs) {
    my $cmd
      = shesc("$RealBin/openarc")
        . " -outdir=".shesc($CETUS_OUTDIR)
        . " -preprocessor=".shesc($CPP.$cppOpts)
        . " -emitLLVM"
        . $arcOpts
        . "$inCFileArgs";
    run($cmd, "openarc");
    for my $file (<$CETUS_OUTDIR/*.bc>) {
      my $fileBase = $file; # no dir and no extension
      $fileBase =~ s/(?:.*\/)?([^.]*|.*\.)(?:.*)?/$1/;
      my $outFile;
      if ($compileOnly && $outOptFile) {
        $outFile = $outOptFile;
      }
      else {
        $outFile = "$fileBase.o";
        if (!$compileOnly) {
          $outFile = "$CETUS_OUTDIR/$outFile";
        }
      }
      $outFiles .= " ".shesc($outFile);
      my $optFile = "$CETUS_OUTDIR/opt/$fileBase.bc";
      $cmd = shesc($LLVM_OPT)." -fitl -nvl ".shesc($file)
             ." -o ".shesc($optFile);
      run($cmd, "opt for $file");
      $cmd = shesc($LLVM_LLC)." -filetype=obj".$llcOpts." ".shesc($optFile)
             ." -o ".shesc($outFile);
      run($cmd, "llc for $file");
    }
  }

  # run linker
  if (!$compileOnly) {
    my $cmd = $CC;
    if ($outOptFile) {
      $cmd .= " -o ".$outOptFile;
    }
    $cmd .= $outFiles . $inOFileArgs . $ldOpts;
    run($cmd, "linking");
  }

  return 0;
}

sub run {
  defined(my $cmd = shift(@_)) || croak;
  defined(my $what = shift(@_)) || croak;
  scalar(@_) == 0 || die;
  print STDERR "$0: running $what: $cmd\n";
  return if (system($cmd) == 0);
  if ($? == -1) {
    die "$what failed to execute: $!\n";
  }
  elsif ($? & 127) {
    die "$what died with signal " . ($? & 127) . ", "
        . (($? & 128) ? "with" : "without") . " coredump\n",
  }
  else {
    die "$what exited with value ".($? >> 8)."\n";
  }
}

sub shesc {
  defined(my $arg = shift(@_)) || croak;
  scalar(@_) == 0 || die;
  $arg =~ s/'/'\\''/g;
  return "'$arg'";
}
